buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url = "http://maven.vaadin.com/vaadin-addons"}
    maven { url = "https://maven.vaadin.com/vaadin-prereleases" }
    maven { url  "https://dl.bintray.com/slatequarry/maven" }

}
configurations.compile.exclude module: "spring-boot-starter-tomcat"

dependencies {
    compile("com.vaadin:vaadin-spring-boot-starter:${vaadinSpringVersion}")

    compile("org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}")

    compile("com.byteowls:vaadin-chartjs:${addonVersion}")
    compile ("de.java2html:java2html:5.0")

    testCompile('org.springframework.boot:spring-boot-starter-test')

    compile ("com.vaadin:vaadin-server:${vaadinVersion}")
    compile ("com.vaadin:vaadin-client-compiled:${vaadinVersion}")
    compile ("com.vaadin:vaadin-themes:${vaadinVersion}")
    compile ("com.yahoo.platform.yui:yuicompressor:2.4.8")
}

project.ext.vaadinThemeDir = "VAADIN/themes/${vaadinTheme}"
project.ext.vaadinCssDir = "${project.buildDir}/tmp/styles"

task compileVaadinScss(type: JavaExec) {
    def styleDir = new File("${vaadinCssDir}")
    if (!styleDir.exists()) {
        styleDir.mkdirs()
    }
    classpath = sourceSets.main.compileClasspath
    main = 'com.vaadin.sass.SassCompiler'
    args = [
        "src/main/webapp/${project.vaadinThemeDir}/styles.scss",
        "${vaadinCssDir}/styles.css",
        "-minify:true",
        "-compress:true"
    ]

}

war {
    dependsOn compileVaadinScss
    baseName = project.ext.artifactId

    from ("${vaadinCssDir}") {
        into vaadinThemeDir
    }

    excludes = [
        "${vaadinThemeDir}/*.scss",
        "${vaadinThemeDir}/*.cache"
    ]

    // Add sources
    webInf {
      into('classes'){
        from sourceSets.main.allJava
       }
    }
}

bootRun {
    addResources = true
    systemProperties System.properties
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
